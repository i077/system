#!/usr/bin/env fish
set _dir (dirname (status filename))
source $_dir/_lib.fish

# Show the contents of $argv[1], depending on the type.
# If nothing is passed, act like bat (and read from STDIN)

if test -z $argv[1]
    bat
else if ! test -e $argv[1]
    log_error "File doesn't exist."
    exit 2
else if test -L $argv[1]
    log_minor "File is a link to "(readlink -m $argv[1])
    show (readlink -m $argv[1]) $argv[2..-1]
else if test -d $argv[1]
    exa -l $argv
else if test -f $argv[1]
    bat $argv
else
    # Not sure how to handle other types at this point, so just pass them to stat
    stat $argv[1]
end
