#!/usr/bin/env fish
set _dir (dirname (status filename))
source $_dir/_lib.fish

# Show the contents of $argv[1], depending on the type.
# If nothing is passed, act like bat (and read from STDIN)

if test -z $argv[1]
    bat
else if ! test -e $argv[1]
    # Maybe I pressed ENTER a little too soon after TAB...
    # If there were multiple completions, prompt to pick one
    if stat $argv[1]* >/dev/null
        set newarg1 (for f in $argv[1]*; echo $f; end | fzf --reverse --height 10)
        if test -z $newarg1
            exit 1
        end
        show $newarg1 $argv[2..-1]
    else
        log_error "File doesn't exist."
        exit 2
    end
else if test -L $argv[1]
    log_minor "File is a link to "(readlink -f $argv[1])
    show (readlink -f $argv[1]) $argv[2..-1]
else if test -d $argv[1]
    exa -l $argv
else if test -f $argv[1]
    # If the file is a binary, just pass it to file instead
    if file --brief --mime $argv[1] | grep -q "charset=binary"
        file $argv
    else
        bat $argv
    end
else
    # Not sure how to handle other types at this point, so just pass them to stat
    stat $argv[1]
end
