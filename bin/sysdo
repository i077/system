#!/usr/bin/env fish

# ----- FORMATTING -----
set red (tput setaf 1)
set green (tput setaf 2)
set white (tput setaf 15)
set purple (tput setaf 5)
set bold (tput bold)
set resf (tput sgr0)

function log_step
    echo $bold$white">> $argv[1]"$resf
end

function log_minor
    echo $white":: "$argv[1]$resf
end

function log_error
    echo $bold$red":: error: "$resf$red$argv[1]$resf
end

function log_ok
    echo $green":: "$argv[1]$resf
end

function ask_yesno
    # Workaround to remove the mode indicator
    function fish_mode_prompt; end

    read -n 1 -P $purple"?? "$argv[1]" (y/N) "$resf reply
    if test $reply = y
        return 0
    end
    return 1
end

# ----- SETUP -----
set flakeRoot (dirname (dirname (status filename)))
set flakeAttr (hostname)

if not set -q IN_NIX_SHELL
    log_step "\$IN_NIX_SHELL not set. Rerunning in a nix shell..."
    exec nix-shell $flakeRoot/shell.nix --run (status filename)" $argv"
end

# Format to 100 columns becauase this is the 21st century
set nixfmt_width 100

cd $flakeRoot

# ----- SUBCOMMANDS -----
function usage
    echo -e $bold"Usage:"$resf" sysdo [flags] [verb] [options]\n"
    echo $bold"Flags:"$resf
    echo "    -C, --no-check      Don't run checks when building configuration"
    echo "    -r, --review        Review changes in configuration before activating"
    echo
    echo $bold"Verbs:"$resf
    echo "        help            Print this help message"
    echo "        clean           Clean up nix build outputs"
    echo "        check           Run checks on this repository"
    echo "     s, switch          Build, activate, and add boot entry for the current configuration"
    echo "     b, boot            Build and add boot entry for the current configuration"
    echo "     t, test            Build and activate the current configuration"
    echo "        build           Build the current configuration"
    echo "        update          Update flake inputs"
    echo "    sh, shell           Spawn a devShell (use -c to specify a command)"
    echo "        repl            Start nix repl with flake"
    echo "        git             Execute a git command in this repository"
    echo "        gc              Delete unreachable store paths"
    echo "        GC [D]          Delete generations older than D days (60 by default)"
    echo "        install N       Install N's configuration to /mnt"
    echo "    up, upgrade         Run update and switch"
    echo "        ub              Run update and boot"
    echo "        ut              Run update and test"
end

function clean
    log_step "Cleaning up build outputs..."
    [ -L result ] && rm -f result
end

function check
    log_step "Running checks..."

    # Check that nix files are formatted properly (and offer to reformat)
    if test -z $_flag_format
        log_minor "Checking formatting with nixfmt..."
    else
        log_minor "Formatting with nixfmt..."
    end
    set -l unformatted_files \
        (find . -name '*.nix' \
            # Exclude files not handled well by nixfmt
            ! -path ./modules/editors/neovim/default.nix \
            -exec nixfmt -c -w $nixfmt_width {} + &| \
            grep "not formatted" | sed -e "s/\(.*\): not formatted/\1/")
    # If there are any unformatted files...
    if test (count $unformatted_files) -gt 0
        log_error "The files below aren't formatted properly:"
        for f in $unformatted_files; echo "  $f"; end
        if ask_yesno "Format these files and amend last commit?"
            log_step "Formatting files..."
            nixfmt -w $nixfmt_width $unformatted_files
            git add $unformatted_files
            git commit --amend --no-edit
        else
            exit 1
        end
    end

    # Check that working tree is clean
    log_minor "Checking for uncommitted changes..."
    set -l unclean_files (git ls-files -m -o -d --exclude-standard)
    if test -n "$unclean_files"
        log_error "Git working tree is unclean. Commit or stash changes to these files:"
        for f in (git ls-files --exclude-standard -o); echo " $f"; end
        git diff --name-only | sed -e "s/\(.*\)/ \1/"
        exit 1
    end

    log_ok "Everything looks good."
end

function _rebuild_after_review
    test -z $_flag_no_check && check
    # If the step involves activating, run dry-activate first
    if contains $argv[1] "test" "boot" "switch"
        log_step "Building configuration..."
        nixos-rebuild dry-activate # This will leave a ./result symlink
        if not ask_yesno "Continue with the above changes?"
            exit 1
        end

        switch $argv[1]
            case "switch"
                log_step "Adding boot entry for and activating configuration..."
                sudo ./result/bin/switch-to-configuration switch
            case "boot"
                log_step "Adding boot entry for configuration..."
                sudo ./result/bin/switch-to-configuration boot
            case "test"
                log_step "Activating configuration..."
                sudo ./result/bin/switch-to-configuration test
        end
        clean
        exit 0
    end

    # Otherwise, just pass to _rebuild
    _rebuild $argv[1]
end

function _rebuild
    test -z $_flag_no_check && check
    switch $argv[1]
        case "switch"
            log_step "Building, activating, and adding boot entry for configuration..."
            sudo nixos-rebuild switch
        case "boot"
            log_step "Building and adding boot entry for configuration..."
            sudo nixos-rebuild boot
        case "test"
            log_step "Building and activating configuration..."
            sudo nixos-rebuild --fast test
        case "build"
            log_step "Building configuration..."
            nixos-rebuild build --flake "$flakeRoot#$flakeAttr"
    end
end

function update
    log_step "Updating flake inputs..."
    set lastCommit (git log --format=%h -1)
    nix flake update --recreate-lock-file --commit-lock-file
    if test lastCommit != (git log --format=%h -1)
        # Print which inputs were updated
        log_ok "Updated inputs:"
        git log --format=%B -1 | grep "Updated" | \
            sed "s/\* Updated '\(.*\)': '.*\([0-9a-f]\{7\}\)[0-9a-f]\{33\}' -> '.*\([0-9a-f]\{7\}\)[0-9a-f]\{33\}'/     \1: \2 -> \3/"
    else
        # No flake inputs were changed
        log_ok "No flake inputs were updated."
    end
end

function shell
    nix develop $flakeRoot $argv
end

function repl
    nix repl https://github.com/edolstra/flake-compat/archive/master.tar.gz --arg src $flakeRoot
end

function git
    command git -C $flakeRoot $argv
end

function upgrade
    update
    switch_
end

function gc
    log_step "Removing unreachable store paths..."
    nix-collect-garbage
end

function GC
    if test -z $argv[1]
        set period 60
    else
        set period $argv[1]
    end
    log_step "Removing generations older than $period days..."
    sudo nix-collect-garbage --delete-older-than {$period}d
end

function install
    # Validate arguments
    if test -z $argv[1]
        log_error "No device name given."
        exit 121
    end

    test -z $_flag_no_check && check

    set prefix /mnt
    # Check for device config
    log_step "Bootstrapping configuration for this device..."
    if not mountpoint -q $prefix
        log_error "Nothing mounted at /mnt."
        exit 1
    else if not test -f $flakeRoot/hosts/$argv[1]/default.nix
        log_error "No device configuration found for $argv[1]."
    end

    log_minor "Generating hardware configuration..."
    nixos-generate-config --show-hardware-config > $flakeRoot/hosts/$argv[1]/hardware-configuration.nix
    # Commit since tree needs to be clean
    git add hosts/$argv[1]/hardware-configuration.nix
    git commit -m "Add device config for $argv[1]"

    # Build and install config
    log_step "Building config and installing to to $prefix..."
    sudo nixos-install --root $prefix --flake $flakeRoot#$argv[1]
    log_ok "Done installing. You can reboot into the installed system."
end

# Parse flags and make them global
argparse -s "C/no-check" "r/review" -- $argv
set -g _flag_no_check $_flag_no_check

# Set rebuild function based on -r flag
if test -z $_flag_review
    set rebuild _rebuild
else
    set rebuild _rebuild_after_review
end

# Parse rest of arguments
switch $argv[1]
    case clean check update upgrade gc repl
        $argv[1]
    case shell git GC install
        $argv[1] $argv[2..-1]
    case "switch" "boot" "test" "build"
        $rebuild $argv[1]
    case s
        $rebuild switch
    case b
        $rebuild boot
    case t
        $rebuild test
    case sh
        shell $argv[2..-1]
    case up
        upgrade
        $rebuild switch
    case ub
        update
        $rebuild boot
    case ut
        update
        $rebuild test
    case help ''
        usage
    case '*'
        log_error "Unknown verb: $argv[1]"
        exit 121
end

# vim: set ft=fish:
